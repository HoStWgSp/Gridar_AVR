/*
* Данный макрос используется для вывода на дисплей строки из програмной памяти
* Не забываем данные регистра привести в соответствие с ASCII
*/
.macro HD44780U_Write_cseg_String
	
	push zh									; вталкиваем zh в STACK
	push zl									; вталкиваем zl в STACK
	
	ldi	zh, high(@0*2)						; записываем старший байт адреса @0 в zh
	ldi	zl, low(@0*2)						; записываем младший байт адреса @0 в zl

	_call HD44780U_Write_cseg_String_act	; переходим

	pop zl									; выталкиваем zl из STACK
	pop zh									; выталкиваем zh из STACK

.endm
//------------------------------------------------------------------------------------
.macro HD44780U_Write_dseg_String
	
	push zh									; вталкиваем zh в STACK
	push zl									; вталкиваем zl в STACK
	
	ldi	zh, high(@0)						; записываем старший байт адреса @0 в zh
	ldi	zl, low(@0)							; записываем младший байт адреса @0 в zl

	_call HD44780U_Write_dseg_String_act	; переходим

	pop zl									; выталкиваем zl из STACK
	pop zh									; выталкиваем zh из STACK

.endm
//____________________________________________________________________________________
//____________________________________________________________________________________
HD44780U_Write_cseg_String_act:
	
	push r17						; вталкиваем r17 в STACK
	push r16						; вталкиваем r16 в STACK

	lpm	r17, z+						; загружаем байт из z с пост-инкрементом в r17

	Typing_cseg_String:
		lpm	r16, z+					; загружаем байт из z с пост-инкрементом в r16
		HD44780U_Send_data r16		; отсылаем данные
		HD44780U_DDRAM_Increment	; проверяем на окончание строки
		dec	r17						; уменьшаем r17 на 1
		brne Typing_cseg_String		; если не равно 0, то переходим

	pop r16							; выталкиваем r16 из STACK
	pop r17							; выталкиваем r17 из STACK

ret									; выход из подпрограммы
//------------------------------------------------------------------------------------
HD44780U_Write_dseg_String_act:
	
	push r17						; вталкиваем r17 в STACK
	push r16						; вталкиваем r16 в STACK

	ld	r17, z+						; загружаем байт из z с пост-инкрементом в r17

	Typing_dseg_String:
		ld	r16, z+					; загружаем байт из z с пост-инкрементом в r16
		HD44780U_Send_data r16		; отсылаем данные
		HD44780U_DDRAM_Increment	; проверяем на окончание строки
		dec	r17						; уменьшаем r17 на 1
		brne Typing_dseg_String		; если не равно 0, то переходим

	pop r16							; выталкиваем r16 из STACK
	pop r17							; выталкиваем r17 из STACK

ret	
