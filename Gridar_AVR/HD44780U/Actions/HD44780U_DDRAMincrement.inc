/*
* Данный макрос используется для проверки окончания строки дисплея
* и если закончиласть, то перемещает курсор на начало следующей строки
*/
.macro HD44780U_DDRAM_Increment
	_call HD44780U_Address_DDRAM_Increment_act
.endm
//____________________________________________________________________________________
//____________________________________________________________________________________
HD44780U_Address_DDRAM_Increment_act:

	push r19									; вталкиваем r19 в STACK

	READ_REG HD44780U_Address_DDRAM, r19		; выгружаем HD44780U_Address_DDRAM в r19

	inc r19										; увеличиваем r19 на 1

	#ifdef HD44780U_LCD_2x16
		cpi r19, $10							; сравниваем r19 с 16
		brne row2								; если не равны, то переходим

		row1:
			HD44780U_Set_Cursor 2, 1			; устанавливаем курсор на вторую строку на первый символ
			pop r19								; выталкиваем r16 из STACK
			ret									; выходим из подпрограммы

		row2:
			cpi r19, $50						; сравниваем r19 с 80
			brne finish							; если не равны, то переходим
			HD44780U_Set_Cursor 1, 1			; устанавливаем курсор на первую строку на первый символ
			pop r19								; выталкиваем r16 из STACK
			ret									; выходим из подпрограммы

	#elif HD44780U_LCD_Size_4x20
		cpi r19, $14							; сравниваем r19 с 20
		brne row2								; если не равны, то переходим

		row1:
			HD44780U_Set_Cursor 2, 1			; устанавливаем курсор на вторую строку на первый символ
			pop r19								; выталкиваем r16 из STACK
			ret									; выходим из подпрограммы

		row2:
			cpi r19, $54						; сравниваем r19 с 84
			brne row3							; если не равны, то переходим
			HD44780U_Set_Cursor 3, 1			; устанавливаем курсор на третью строку на первый символ
			pop r19								; выталкиваем r16 из STACK
			ret									; выходим из подпрограммы

		row3:
			cpi r19, $28						; сравниваем r19 с 40
			brne row4							; если не равны, то переходим
			HD44780U_Set_Cursor 4, 1			; устанавливаем курсор на четвертую строку на первый символ
			pop r19								; выталкиваем r16 из STACK
			ret									; выходим из подпрограммы

		row4:
			cpi r19, $68						; сравниваем r19 с 104
			brne finish							; если не равны, то переходим
			HD44780U_Set_Cursor 1, 1			; устанавливаем курсор на первую строку на первый символ
			pop r19								; выталкиваем r16 из STACK
			ret									; выходим из подпрограммы

	#endif

	finish:
		WRITE_REG HD44780U_Address_DDRAM, r19	; загружаем r16 в HD44780U_Address_DDRAM
		pop r19									; выталкиваем r16 из STACK
ret												; выходим из подпрограммы 
